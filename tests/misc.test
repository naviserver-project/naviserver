# -*- Tcl -*-

package require tcltest 2.2
namespace import -force ::tcltest::*

::tcltest::configure {*}$argv

testConstraint nomacos [expr {$::tcl_platform(os) ne "Darwin"}]

#######################################################################################
#  test ns_parsefieldvalue
#######################################################################################

test ns_random-1.0 {ns_rand with 1 arg - integer} -body {
    set rand [ns_rand 10]
    return [list [expr {$rand < 10}] [string is integer -strict $rand] ]
} -result {1 1}

test ns_random-1.1 {ns_rand with 0 arg - float} -body {
    set rand [ns_rand]
    return [list [expr {$rand < 1}] [string is double -strict $rand] ]
} -result {1 1}

#######################################################################################
#  test ns_fmttime
#######################################################################################

test ns_fmttime-1.0 {ns_fmttime without format} -body {
    #
    # "ns_fmttime" depends on the local time-zone. Since we cannot
    # pass the timezone as an argument, we make a rough test here.
    #
    return [llength [ns_fmttime 1563812758]]
} -result {5}

test ns_fmttime-1.1 {ns_rand with 0 arg - float} -body {
    #
    # The year information is in all timezones for this timestamp
    # identical.
    #
    return [ns_fmttime 1563812758 "%Y 00:00"]
} -result {2019 00:00}

#######################################################################################
#  test ns_trim
#######################################################################################

test ns_trim-0.0 {ns_trim without arguments} -body {
    ns_trim
} -returnCodes error -result {wrong # args: should be "ns_trim ?-subst? ?-delimiter delimiter? ?-prefix prefix? ?--? text"}
test ns_trim-0.1 {ns_trim with invalid delimiter} -body {
    ns_trim -delimiter aa "hello world"
} -returnCodes error -result {invalid arguments: -delimiter must be a single character}
test ns_trim-0.2 {ns_trim with delimiter and prefix} -body {
    ns_trim -delimiter a -prefix b "hello world"
} -returnCodes error -result {invalid arguments: either -prefix or -delimiter can be specified}

test ns_trim-1.1 {nothing to trim} -body {
    string map {\n \\n} [ns_trim "line1\nline2"]
} -result {line1\nline2}
test ns_trim-1.2 {trim spaces} -body {
    string map {\n \\n} [ns_trim " line1\n  line2\nline3\n "]
} -result {line1\nline2\nline3\n}
test ns_trim-1.3 {trim with delimiter} -body {
    string map {\n \\n} [ns_trim -delimiter : " : line1\n  : line2\n    : line3\n :"]
} -result { line1\n line2\n line3\n}
test ns_trim-1.4 {trim with delimiter and leading newline} -body {
    string map {\n \\n} [ns_trim -delimiter : "\n : line1\n  : line2\n    : line3\n :"]
} -result { line1\n line2\n line3\n}
test ns_trim-1.5 {trim with delimiter and no occurence of delimiter} -body {
    string map {\n \\n} [ns_trim -delimiter ! "\n : line1\n  : line2\n    : line3\n :"]
} -result {: line1\n: line2\n: line3\n:}

test ns_trim-2.1 {trim with prefix} -body {
    string map {\n \\n} [ns_trim -prefix "> " "\n>  line1\nline2\n>   line3\n> "]
} -result {\n line1\nline2\n  line3\n}
test ns_trim-2.2 {trim with delimiter and no occurence of prefix} -body {
    string map {\n \\n} [ns_trim -prefix > "\n : line1\n  : line2\n    : line3\n :"]
} -result {\n : line1\n  : line2\n    : line3\n :}

#######################################################################################
#  test ns_quotehtml
#######################################################################################

test ns_quotehtml {ns_quotehtml with typical value} -body {
    return [ns_quotehtml {<span class="foo">}]
} -result {&lt;span class=&#34;foo&#34;&gt;}

#######################################################################################
#  test ns_strcoll
#######################################################################################

test ns_strcoll-1.0.0 {ns_strcoll without locale (assuming en_US.UTF-8)} -body {
    return [expr {[ns_strcoll Bär Bor] < 0}]
} -result 1

test ns_strcoll-1.0.1 {ns_strcoll with locale C} -body {
    return [expr {[ns_strcoll -locale C Bär Bor] > 0}]
} -result 1

test ns_strcoll-1.0.2 {ns_strcoll with locale en_US.UTF-8 } -body {
    return [expr {[ns_strcoll -locale en_US.UTF-8 Bär Bor] < 0}]
} -result 1

test ns_strcoll-1.1 {ns_strcoll without locale specified} -body {
    set l {Önce Ince Once Ance Adam Çengel Art Ceb Dora Pravda Правда Omikron ό Zeppelin Bar Bor Bär}
    return [lsort -command ns_strcoll $l]
} -result {Adam Ance Art Bar Bär Bor Ceb Çengel Dora Ince Omikron Once Önce Pravda Zeppelin ό Правда}

test ns_strcoll-1.2 {ns_strcoll with locale en_US.UTF-8} -body {
    set l {Önce Ince Once Ance Adam Çengel Art Ceb Dora Pravda Правда Omikron ό Zeppelin Bar Bor Bär}
    return [lsort -command {ns_strcoll -locale en_US.UTF-8 --} $l]
} -result {Adam Ance Art Bar Bär Bor Ceb Çengel Dora Ince Omikron Once Önce Pravda Zeppelin ό Правда}

# For unknown reasons, macOS has a different sorting order:
# Linux sorts "-a" between "1" and "b" (i.e. ignores dash), while
# macOS sorts "-a" before "1"
test ns_strcoll-1.3 {sort with leading dashes, without locale specified} \
     -constraints nomacos -body {
        set l {b 1 -a}
        return [lsort -command ns_strcoll $l]
    } -result {1 -a b}

test ns_strcoll-1.4 {sort with leading dashes, with locale en_US.UTF-8} \
    -constraints nomacos -body {
        set l {b 1 -a}
        return [lsort -command {ns_strcoll -locale en_US.UTF-8 --} $l]
    } -result {1 -a b}

#######################################################################################
#  test ns_valid_utf8
#######################################################################################

test ns_valid_utf8-1.0 {ns_valid_utf8 with typical value} -body {
    return [ns_valid_utf8 "hello world"]
} -result 1

test ns_valid_utf8-1.1 {invalid utf8, containing x85 at the end} -body {
    return [ns_valid_utf8 "forschungsprojek\x85"]
} -result 0

test ns_valid_utf8-1.2 {valid utf8, containing x85 at the end} -body {
    return [ns_valid_utf8 "forschungsprojek\xc3\x85"]
} -result 1


#
# Local variables:
#    mode: tcl
#    tcl-indent-level: 4
#    indent-tabs-mode: nil
# End:
