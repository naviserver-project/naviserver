# -*- Tcl -*-
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# The Initial Developer of the Original Code and related documentation
# is America Online, Inc. Portions created by AOL are Copyright (C) 1999
# America Online, Inc. All Rights Reserved.
#
#

package require tcltest 2.2
namespace import -force ::tcltest::*
if {$::tcl_version < 8.6} {package require try}

::tcltest::configure {*}$argv

if {[ns_config test listenport]} {
    testConstraint serverListen true
}
testConstraint http09 true

#ns_logctl severity Debug(ns:driver) on

test http_chunked-1.1 {
    ADP streaming w/chunks to HTTP/1.1 client
} -constraints {serverListen http09} -body {
    nstest::http-0.9 -http 1.1 -setheaders {connection keep-alive} \
                -getbody 1 -getheaders {transfer-encoding connection content-length} \
        GET /http_chunked.adp?stream=1
} -result "200 chunked keep-alive {} {a\n0123456789\n5\n01234\n0\n\n}"

test http_chunked-1.2 {
    ADP with short buffered response
} -constraints {serverListen http09} -body {
    nstest::http-0.9 -http 1.1 -setheaders {connection keep-alive} \
                -getheaders {transfer-encoding connection content-length} -getbody t \
        GET /http_chunked.adp?stream=0
} -result "200 {} keep-alive 15 012345678901234"

test http_chunked-1.3 {
    ADP with longer partly-buffered response and auto-streaming
} -constraints {serverListen http09} -body {
    nstest::http-0.9 -http 1.1 -setheaders {connection keep-alive} \
                -getheaders {transfer-encoding connection content-length} -getbody t \
        GET /http_chunked.adp?stream=0&bufsize=8
} -result "200 chunked keep-alive {} {a\n0123456789\n5\n01234\n0\n\n}"


test http_chunked-1.4 {
    ADP auto-streaming to HTTP/1.0 client
} -constraints serverListen -body {
    nstest::http -http 1.0 -setheaders {connection keep-alive} \
        -getheaders {transfer-encoding connection content-length} -getbody t \
        GET /http_chunked.adp?stream=1&bufsize=8
} -returnCodes {error ok} -result {200 {} close {} 012345678901234}


test http_chunked-2.1 {
    Tcl streaming w/chunks to HTTP/1.1 client
} -constraints {serverListen http09} -setup {
    ns_register_proc GET /tclchunked {
        ns_headers 200 text/plain
        ns_write 0123456789
        ns_write 01234
    }
} -body {
    nstest::http-0.9 -http 1.1 -getheaders {transfer-encoding content-length} -getbody 1 \
        GET /tclchunked
} -cleanup {
    ns_unregister_op GET /tclchunked
} -result "200 chunked {} {a\n0123456789\n5\n01234\n0\n\n}"


test http_chunked-2.1.1 {
    Tcl streaming multiple binary buffers w/chunks to HTTP/1.1 client
} -constraints {serverListen http09} -setup {
    ns_register_proc GET /tclchunked {
        ns_headers 200 text/plain
        ns_write [binary format a5 01234] [binary format a5 56789]
        ns_write [binary format a5 01234]
    }
} -body {
    nstest::http-0.9 -http 1.1 -getheaders {transfer-encoding content-length} -getbody 1 \
        GET /tclchunked
} -cleanup {
    ns_unregister_op GET /tclchunked
} -result "200 chunked {} {a\n0123456789\n5\n01234\n0\n\n}"


test http_chunked-2.2 {Tcl streaming to HTTP/1.0 client} -constraints serverListen -setup {
    ns_register_proc GET /tclchunked {
        ns_headers 200 text/plain
        ns_write 0123456789
        ns_write 01234
    }
} -body {
    nstest::http -http 1.0 -getheaders {transfer-encoding content-length} -getbody 1 \
        GET /tclchunked
} -cleanup {
    ns_unregister_op GET /tclchunked
} -result {200 {} {} 012345678901234}


test http_chunked-2.3 {
    Tcl response of known size to HTTP/1.1 client
} -constraints serverListen -setup {
    ns_register_proc GET /tclchunked {ns_return 200 text/plain 01234}
} -body {
    nstest::http -http 1.1 -getheaders {transfer-encoding content-length} -getbody 1 \
        GET /tclchunked
} -cleanup {
    ns_unregister_op GET /tclchunked
} -result {200 {} 5 01234}


test http_chunked-2.4.0 {

    PUT request with invalid chunked content.
    providing a negative length

} -constraints {serverListen http09} -setup {
    ns_register_proc PUT /tclchunked {
        set content [ns_conn content]
        #ns_log notice "PUT received <$content>"
        ns_return 200 text/plain $content
    }
} -body {
    nstest::http-0.9 -http 1.1 \
        -omitcontentlength 1 \
        -setheaders {transfer-encoding Chunked} \
        -getheaders {transfer-encoding content-length} -getbody 1 \
        PUT /tclchunked "-1\r\nchunk\r\n0\r\n\r\n"
} -cleanup {
    ns_unregister_op PUT /tclchunked
} -result {400 {} {}}


test http_chunked-2.4.1 {

    PUT request with chunked data and invalid length
    (no hex encoded data)

} -constraints {serverListen http09} -setup {
    ns_register_proc PUT /tclchunked {
        set content [ns_conn content]
        #ns_log notice "PUT received <$content>"
        ns_return 200 text/plain $content
    }
} -body {
    nstest::http-0.9 -http 1.1 \
        -omitcontentlength 1 \
        -setheaders {transfer-encoding Chunked} \
        -getheaders {transfer-encoding content-length} -getbody 1 \
        PUT /tclchunked "x\r\nD\r\n0\r\n\r\n"
} -cleanup {
    ns_unregister_op PUT /tclchunked
} -result {400 {} {}}


test http_chunked-2.4.2 {

    PUT request with invalid chunked content. Server receives much
    less data than announced via chunk lengthw. Server will wait for
    more data, and runs after some time into a timeout.

} -constraints {serverListen http09} -setup {
    ns_register_proc PUT /tclchunked {
        set content [ns_conn content]
        #ns_log notice "PUT received <$content>"
        ns_return 200 text/plain $content
    }
} -body {
    set result {}
    set errorMsg {}
    try {
        nstest::http-0.9 -http 1.1 \
            -omitcontentlength 1 \
            -setheaders {transfer-encoding Chunked connection close} \
            -getheaders {transfer-encoding content-length} \
            PUT /tclchunked "ffffffff\r\nchunk\r\n0\r\n\r\n"
    } on error {errorMsg} {
    } on ok {result} {
    }
    list errorMsg $errorMsg result $result
} -cleanup {
    ns_unregister_op PUT /tclchunked
    unset result errorMsg
} -result {errorMsg {} result {}}


test http_chunked-2.4.3 {

    PUT request with chunked data and a chunk extension.

} -constraints {serverListen http09} -setup {
    ns_register_proc PUT /tclchunked {
        set content [ns_conn content]
        #ns_log notice "PUT received <$content>"
        ns_return 200 text/plain $content
    }
} -body {
    nstest::http-0.9 -http 1.1 \
        -omitcontentlength 1 \
        -setheaders {transfer-encoding Chunked} \
        -getheaders {transfer-encoding content-length} -getbody 1 \
        PUT /tclchunked "1;last\r\nD\r\n0\r\n\r\n"
} -cleanup {
    ns_unregister_op PUT /tclchunked
} -returnCodes {ok} -result {200 {} 1 D}



test http_chunked-3.1 {

    PUT with transfer-encoding Chunked and unknown content-length (but
    provided X-Expected-Entity-Length). This is the format that WebDAV
    client of macOS uses (via Finder).

} -constraints {serverListen http09} -setup {
    ns_register_proc PUT /tclchunked {
        set content [ns_conn content]
        #ns_log notice "PUT received <$content>"
        ns_return 200 text/plain $content
    }
} -body {
    nstest::http-0.9 -http 1.1 \
        -omitcontentlength 1 \
        -setheaders {transfer-encoding Chunked X-Expected-Entity-Length 13} \
        -getheaders {transfer-encoding content-length} -getbody 1 \
        PUT /tclchunked "0A\r\n1234567890\r\n03\r\nabc"
} -cleanup {
    ns_unregister_op PUT /tclchunked
} -result {200 {} 13 1234567890abc}


cleanupTests

# Local variables:
#    mode: tcl
#    tcl-indent-level: 4
#    indent-tabs-mode: nil
# End:
