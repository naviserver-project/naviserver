# -*- Tcl -*-

package require tcltest 2.2
namespace import -force ::tcltest::*

::tcltest::configure {*}$argv

if {[ns_config test listenport]} {
    testConstraint serverListen true
}

test ns_writer-1.1 {basic syntax: plain call} -body {
     ns_writer
} -returnCodes error -result {wrong # args: should be "ns_writer command ?args?"}

test ns_writer-1.2 {basic syntax: wrong argument} -body {
     ns_writer ?
} -returnCodes error -result {bad subcmd "?": must be list, size, streaming, submit, submitfile, or submitfiles}

test ns_writer-1.3 {basic operation} -body {
    catch {ns_writer list -foo} errorMsg
    set _ $errorMsg
} -match exact -result {wrong # args: should be "ns_writer list ?-server server?"}

test ns_writer-1.4 {basic operation} -body {
    catch {ns_writer list -server test foo} errorMsg
    set _ $errorMsg
} -match exact -result {wrong # args: should be "ns_writer list ?-server server?"}

test ns_writer-2.1 {basic operation} -body {
    expr {[ns_writer list] eq ""}
} -match exact -result 1

test ns_writer-2.2 {basic operation} -body {
    expr {[ns_writer list -server test] eq ""}
} -match exact -result 1

test ns_writer-2.3 {basic operation} -body {
    catch {expr {[ns_writer list -server foo] eq ""}} errorMsg
    set _ $errorMsg
} -match exact -result {invalid server: 'foo'}


#
# ns_writer size
#  - with and without connection
#  - with and without "-driver" argument
#
test ns_writer-3.1.0 {basic operation} -body {
    ns_writer size
} -returnCodes error -match exact -result {no connection}

test ns_writer-3.1.1 {basic operation} -body {
    ns_writer size -driver nssock
} -returnCodes {ok error} -match exact -result {1026}

test ns_writer-3.1.2 {basic operation} -constraints serverListen -setup {
    ns_register_proc GET /foo {
        ns_return 200 text/plain [ns_writer size]
    }
} -body {
    nstest::http -getbody 1 GET /foo
} -cleanup {
    ns_unregister_op GET /foo
} -returnCodes {error ok} -match exact -result {200 1026}


#
# ns_writer streaming
#  - with and without connection
#  - with and without "-driver" argument
#
test ns_writer-3.2.0 {basic operation} -body {
    ns_writer streaming
} -returnCodes error -match exact -result {no connection}

test ns_writer-3.2.1 {basic operation} -body {
    ns_writer streaming -driver nssock
} -returnCodes {ok error} -match exact -result {0}

test ns_writer-3.2.2 {basic operation} -constraints serverListen -setup {
    ns_register_proc GET /foo {
        ns_return 200 text/plain [ns_writer streaming]
    }
} -body {
    nstest::http -getbody 1 GET /foo
} -cleanup {
    ns_unregister_op GET /foo
} -returnCodes {error ok} -match exact -result {200 0}


#
# ns_writer submitfile
#
test ns_writer-4.1.0 {simple submitfile} -constraints serverListen -setup {
    ns_register_proc GET /file {
        ns_writer submitfile [ns_pagepath 10bytes]
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match exact -result {200 0123456789}

test ns_writer-4.1.1 {submitfile with offset} -constraints serverListen -setup {
    ns_register_proc GET /file {
        ns_writer submitfile -offset 3 [ns_pagepath 10bytes]
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match exact -result {200 3456789}

test ns_writer-4.1.2 {submitfile with offset and size} -constraints serverListen -setup {
    ns_register_proc GET /file {
        ns_writer submitfile -offset 3 -size 4 [ns_pagepath 10bytes]
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match exact -result {200 3456}

#
# ns_writer submitfiles
#
test ns_writer-4.2.0 {single file chunk} -constraints serverListen -setup {
    ns_register_proc GET /file {
        set entry [list filename [ns_pagepath 10bytes]]
        ns_writer submitfiles [list $entry $entry]
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match exact -result {200 01234567890123456789}


test ns_writer-4.2.1.0 {error: empty filespec} -constraints serverListen -setup {
    ns_register_proc GET /file {
        catch {ns_writer submitfiles {}} result
        ns_return 500 text/plain $result
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match glob -result {*The provided list has to contain at least one file spec*}

test ns_writer-4.2.1.2 {error: two empty filespecs} -constraints serverListen -setup {
    ns_register_proc GET /file {
        set entry [list]
        catch {ns_writer submitfiles [list $entry $entry]} result
        ns_return 500 text/plain $result
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match glob -result {*missing filename in dict*}

test ns_writer-4.2.1.3 {error: filespec with too large offset} -constraints serverListen -setup {
    ns_register_proc GET /file {
        set entry [list filename [ns_pagepath 10bytes] -offset 20]
        catch {ns_writer submitfiles [list $entry $entry]} result
        ns_return 500 text/plain $result
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match glob -result {*offset must be a positive value less or equal filesize*}

test ns_writer-4.2.1.4 {error: filespec with too large size} -constraints serverListen -setup {
    ns_register_proc GET /file {
        set entry [list filename [ns_pagepath 10bytes] -size 20]
        catch {ns_writer submitfiles [list $entry $entry]} result
        ns_return 500 text/plain $result
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match glob -result {*offset + size must be less or equal filesize*}



test ns_writer-4.2.2 {two file chunks} -constraints serverListen -setup {
    ns_register_proc GET /file {
        set entry [list filename [ns_pagepath 10bytes] -offset 3]
        ns_writer submitfiles [list $entry $entry]
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match exact -result {200 34567893456789}


test ns_writer-4.2.3 {two file chunks with offset and size} -constraints serverListen -setup {
    ns_register_proc GET /file {
        set entry [list filename [ns_pagepath 10bytes] -offset 3 -size 4]
        ns_writer submitfiles [list $entry $entry]
    }
} -body {
    nstest::http -getbody 1 GET /file
} -cleanup {
    ns_unregister_op GET /file
} -returnCodes {error ok} -match exact -result {200 34563456}

cleanupTests

# Local variables:
#    mode: tcl
#    tcl-indent-level: 4
#    indent-tabs-mode: nil
# End:
