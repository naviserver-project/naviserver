# -*- Tcl -*-

package require tcltest 2.2
namespace import -force ::tcltest::*

::tcltest::configure {*}$argv

testConstraint with_deprecated [dict get [ns_info buildinfo] with_deprecated]
#ns_log notice XXXXXXXXXXXXXXXXXXX  [ns_info buildinfo]

#######################################################################################
#  Syntax Tests
#######################################################################################

test ns_config-1.1 {syntax: ns_config} -body {
    ns_config
} -returnCodes error -result {wrong # args: should be "ns_config ?-all? ?-bool? ?-int? ?-min /integer/? ?-max /integer/? ?-exact? ?-set? ?--? /section/ /key/ ?/default/?"}

test ns_configsection-1.0 {syntax: ns_configsection} -body {
    ns_configsection
} -returnCodes error -result {wrong # args: should be "ns_configsection ?-filter unread|defaulted|defaults? ?--? /section/"}

test ns_configsections-1.0 {syntax: ns_configsections} -body {
    ns_configsections ?
} -returnCodes error -result {wrong # args: should be "ns_configsections"}

test ns_library-1.0 {syntax: ns_library} -body {
    ns_library
} -returnCodes error -result {wrong # args: should be "ns_library private|shared ?/module/?"}

test ns_modulepath-1.0 {syntax: ns_modulepath} -body {
    ns_modulepath
} -returnCodes error -result {wrong # args: should be "ns_modulepath /server/ ?/module .../?"}

# The following commands are available only from the configuration file.

test ns_section-1.0 {syntax: ns_section} -body {
    ns_section
} -returnCodes error -result {invalid command name "ns_section"}
# "ns_section" message should be {wrong # args: should be "ns_section ?-update? ?-from /sectionName/? ?-set /set/? ?--? /name/"}

test ns_section-1.0 {syntax: ns_param} -body {
    ns_param
} -returnCodes error -result {invalid command name "ns_param"}
# "ns_section" message should be {wrong # args: should be "ns_param /key/ /value/"}


#######################################################################################
#  Functional Tests
#######################################################################################

test ns_config-2.1 {bool} -body {
    expr {[ns_config -bool ns/testconfig trueval] ? 1 : 0}
} -result 1

test ns_config-2.2 {missing bool} -body {
    ns_config -bool ns/testconfig missing
} -result {}

test ns_config-2.3 {bool default} -body {
    expr {[ns_config -bool ns/testconfig missing y] ? 1 : 0}
} -result 1

test ns_config-2.4 {bool default} -body {
    expr {[ns_config -bool ns/testconfig missing off] ? 1 : 0}
} -result 0

test ns_config-2.5 {bool default, not a bool} -body {
    ns_config -bool ns/testconfig missing huh?
} -returnCodes error -result {expected boolean value but got "huh?"}

test ns_config-2.6 {not a bool} -body {
    ns_config -bool ns/testconfig notbool
} -returnCodes {error ok} -result {expected boolean value but got "this-is-not-a-bool"}

test ns_config-2.7 {not a bool, bool default} -body {
    expr {[ns_config -bool ns/testconfig notbool on] ? 1 : 0}
} -returnCodes {error ok} -result 1

test ns_config-2.8 {not a bool, bool default} -body {
    ns_config -bool ns/testconfig notbool huh?
} -returnCodes error -result {expected boolean value but got "huh?"}



test ns_config-3.1 {int} -body {
    ns_config -int ns/testconfig intval
} -result 42

test ns_config-3.2 {missing int} -body {
    ns_config -int ns/testconfig missing
} -result {}

test ns_config-3.3 {int default} -body {
    ns_config -int ns/testconfig missing 42
} -result 42

test ns_config-3.4 {not an int} -body {
    ns_config -int ns/testconfig missing huh?
} -returnCodes error -result {expected integer but got "huh?"}

test ns_config-3.5 {not an int} -body {
    ns_config -int ns/testconfig notint
} -returnCodes error -result {expected integer but got "this-is-not-an-int"}

test ns_config-3.6 {wide int} -body {
    ns_config -int ns/testconfig wideint
} -result {12345678900}



test ns_config-4.1 {int min} -body {
    ns_config -min 41 ns/testconfig intval
} -result 42

test ns_config-4.2 {int min, no default, out of range} -body {
    ns_config -min 43 ns/testconfig intval
} -returnCodes {error ok} -result {value '42' out of range}

test ns_config-4.3 {int max} -body {
    ns_config -max 43 ns/testconfig intval
} -result 42

test ns_config-4.4 {int max} -body {
    ns_config -max 41 ns/testconfig intval
} -returnCodes error -result {value '42' out of range}

test ns_config-4.5 {int min max} -body {
    ns_config -min 41 -max 43 ns/testconfig intval
} -result 42

test ns_config-4.6 {int min def} -body {
    ns_config -min 43 ns/testconfig intval 43
} -returnCodes {error ok} -result 43

test ns_config-4.7 {int max def} -body {
    ns_config -max 41 ns/testconfig intval 41
}  -returnCodes {error ok} -result 41

test ns_config-4.8 {int max def oob} -body {
    ns_config -max 41 ns/testconfig missing 42
} -returnCodes error -result {value '42' out of range}

test ns_config-4.9 {int min value oob default ok} -body {
    ns_config -min 41 ns/testconfig intval 40
} -returnCodes {error ok} -result 42


test ns_config-5.1 {no exact} -body {
    ns_config ns/testconfig camelcase
} -result ok

test ns_config-5.2 {exact} -body {
    ns_config -exact ns/testconfig CamelCase
} -result ok

test ns_config-5.3 {exact} -body {
    # The config set has the nocase option, so "-exact" is ignored and
    # the key is found.
    ns_config -exact ns/testconfig camelcase
} -result {ok}

test ns_config-5.4 {exact} -body {
    # The config set has the nocase option, so "-exact" is ignored,
    # and the key is found
    ns_config -exact ns/testconfig camelcase default
} -result ok


test ns_config-6.1 {multiple values, return the first one} -body {
    ns_config ns/testconfig multiple
} -result on

test ns_config-6.2 {multiple values, return all} -body {
    ns_config -all ns/testconfig multiple
} -result {on off}

test ns_config-6.3 {multiple values, return bool} -body {
    ns_config -bool ns/testconfig multiple
} -returnCodes {error ok} -result {ns_config: -bool flag implies a single value, but got 2 values}

test ns_config-6.4 {multiple values, return int} -body {
    ns_config -int ns/testconfig multiple
} -returnCodes {error ok} -result {ns_config: -int flag implies a single value, but got 2 values}

test ns_config-6.5 {single value, without -all} -body {
    ns_config ns/testconfig single
} -returnCodes {error ok} -result {hello world}

test ns_config-6.6 {single value, with -all} -body {
    ns_config -all ns/testconfig single
} -returnCodes {error ok} -result {{hello world}}

test ns_config-6.7 {using -all, non-exiting value, no default} -body {
    llength [ns_config -all ns/testconfig missing]
} -returnCodes {error ok} -result 0



test ns_config-7.1 {sections} -body {
    lindex [ns_configsections] 0
} -match regexp -result {t[0-9]+}

test ns_config-7.2 {section} -body {
    ns_set array [ns_configsection ns/testconfig]
} -match glob -result {trueval yes intval 42 *}

test ns_config-7.3 {section missing} -body {
    ns_configsection section/does/not/exist
} -result {}

test ns_config-7.4.0 {section} -body {
    ns_set array [ns_configsection -filter "" ns/testconfig]
} -returnCodes {error ok} -result {bad option "": must be unread, defaulted, or defaults}

test ns_config-7.4.1 {section} -body {
    ns_set array [ns_configsection -filter "unread" ns/testconfig]
} -returnCodes {error ok} -result {trueval yes intval 42 notbool this-is-not-a-bool notint this-is-not-an-int camelcase ok wideint 12345678900 istestserver true single {hello world} multiple on multiple off}

test ns_config-7.4.2 {section} -body {
    ns_set size [ns_configsection -filter "defaulted" ns/server/testvhost]
} -returnCodes {error ok} -result {26}

test ns_config-7.4.3 {section} -body {
    ns_set size [ns_configsection -filter "defaults" ns/server/testvhost]
} -returnCodes {error ok} -result {27}


test ns_config-8.1 {missing -set} -body {
    ns_config ns/testconfig missing missing
} -result {missing}

test ns_config-8.2 {missing -set} -body {
    ns_config ns/testconfig missing
} -result {}

test ns_config-8.3 {missing -set} -body {
    ns_config ns/testconfig missing here
} -result {here}

test ns_config-8.4 {missing -set} -body {
    ns_config ns/testconfig missing here
} -result {here}


#
# ns_configure_variables regression tests
#
test ns_configure_variables-1.0 {syntax: ns_configure_variables} -body {
    ns_configure_variables
} -returnCodes error -match glob -result {wrong # args: should be "?-configRoot /configRoot/? ?-silent /silent/? ?--? /prefix/ /defaultConfig/"}
# "ns_configure_variables" message should be {wrong # args: should be "ns_configure_variables ?-configRoot /directory/? ?-silent /boolean/? ?--? /prefix/ /defaultConfig/"}

#
# Helper: run ns_configure_variables in a clean scope and return a dict
# of selected variables.
#
proc _nscv_run {prefix defaultConfig presets {configRoot ""}} {
    dict for {k v} $presets { set $k $v }
    ns_configure_variables -configRoot $configRoot -silent true $prefix $defaultConfig
    set out [dict create]
    foreach v {home logdir extra_var} {
        if {[info exists $v]} { dict set out $v [set $v] }
    }
    return $out
}

test ns_configure_variables-2.0 {defaults + subst are applied} -body {
    _nscv_run "oacs_" {home "/h" logdir {$home/logs} setupfile ""} {}
} -result [dict create home "/h" logdir "/h/logs"]

test ns_configure_variables-2.1 {environment overrides defaults} -body {
    set ::env(oacs_home) "/envhome"
    _nscv_run "oacs_" {home "/h" logdir {$home/logs} setupfile ""} {}
} -cleanup {
    unset -nocomplain ::env(oacs_home)
} -result [dict create home "/envhome" logdir "/envhome/logs"]

test ns_configure_variables-2.2 {config-file preset overrides environment} -body {
    set ::env(oacs_home) "/envhome"
    _nscv_run "oacs_" {home "/h" setupfile ""} [dict create home "/cfg"]
} -cleanup {
    unset -nocomplain ::env(oacs_home)
} -result [dict create home "/cfg"]

test ns_configure_variables-2.3 {subst uses overridden values (env -> dependent var)} -body {
    set ::env(oacs_home) "/envhome"
    _nscv_run "oacs_" {home "/h" logdir {$home/logs}} [dict create]
} -cleanup {
    unset -nocomplain ::env(oacs_home)
} -result [dict create home "/envhome" logdir "/envhome/logs"]


test ns_configure_variables-3.0 {setupfile resolved relative to explicit configRoot} -body {
    set tmpdir [file normalize [file join [pwd] .tmp-ns-configure-variables]]
    file mkdir $tmpdir
    set setup [file join $tmpdir setup.tcl]
    set f [open $setup w]
    puts $f { set home "/setup" ; set extra_var "from_setup" }
    close $f

    _nscv_run "oacs_" {home "/h" setupfile "setup.tcl"} [dict create] $tmpdir
} -cleanup {
    catch {file delete -force [file normalize [file join [pwd] .tmp-ns-configure-variables]]}
} -returnCodes {error ok} -result [dict create home "/setup" extra_var "from_setup"]


test ns_configure_variables-3.1 {setupfile resolves relative to explicit configRoot} -body {
    set tmpdir [file normalize [file join [pwd] .tmp-ns-configure-variables]]
    file mkdir $tmpdir
    set setup [file join $tmpdir setup.tcl]
    set f [open $setup w]
    puts $f { set extra_var "dir_mode" }
    close $f

    _nscv_run "oacs_" {home "/h" setupfile "setup.tcl"} [dict create] $tmpdir
} -cleanup {
    catch {file delete -force [file normalize [file join [pwd] .tmp-ns-configure-variables]]}
} -result [dict create home "/h" extra_var "dir_mode"]


test ns_configure_variables-3.2 {setupfile can be absolute path} -body {
    set tmpdir [file normalize [file join [pwd] .tmp-ns-configure-variables]]
    file mkdir $tmpdir
    set setup [file join $tmpdir setup.tcl]
    set f [open $setup w]
    puts $f { set extra_var "abs_path" }
    close $f

    _nscv_run "oacs_" [list home "/h" setupfile $setup] [dict create] $tmpdir
} -cleanup {
    catch {file delete -force [file normalize [file join [pwd] .tmp-ns-configure-variables]]}
} -result [dict create home "/h" extra_var "abs_path"]

test ns_configure_variables-3.3 {missing setupfile -> error} -body {
    set tmpdir [file normalize [file join [pwd] .tmp-ns-configure-variables]]
    file mkdir $tmpdir

    _nscv_run "oacs_" {home "/h" setupfile "DOES-NOT-EXIST.tcl"} [dict create] $tmpdir
} -cleanup {
    catch {file delete -force [file normalize [file join [pwd] .tmp-ns-configure-variables]]}
} -returnCodes error -match glob -result {*setupfile not found:*}


rename _nscv_run ""


cleanupTests

# Local variables:
#    mode: tcl
#    tcl-indent-level: 4
#    indent-tabs-mode: nil
# End:
