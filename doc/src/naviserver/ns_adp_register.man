[include version_include.man]
[manpage_begin   ns_adp_register n [vset version]]
[moddesc  {NaviServer Built-in Commands}]

[titledesc {Creating Custom ADP Tags}]

[description]



 These commands enable the definition of custom ADP tags
 (application-specific, XML-like tags) which are expanded whenever ADP
 content (.adp files or ADP strings processed via [cmd ns_adp_parse])
 is processed. Registering application-specific ADP tags is a means to
 include script-driven, application-specific content in a web page,
 providing an alternative to embedding scripts in ADP pages via the
 [const <%] script [const %>] syntax as described in the
 [uri ../../manual/files/adp-overview.html "ADP Overview"] man page.


[para] Registered tags can either be tags with content -- consisting of
 an opening and a closing tag -- or tags defining empty elements, where
 no closing tag is defined. When both the [arg tag] and the
 [arg endtag] are specified in the registering command, the tags must have
 content; when the [arg endtag] is omitted in the registering command,
 empty elements are defined.


[section COMMANDS]

[list_begin definitions]

[call [cmd ns_adp_registeradp] \
	[arg tag]  \
	[opt [arg endtag]] \
	[arg adpstring]]

 Registers an ADP snippet [arg adpstring] to be included when the
 specified [arg tag] is encountered while parsing the ADP content. The
 [arg tag] argument specifies the tag that will trigger the inclusion
 of the parsed ADP fragment [arg adpstring].

[para] If the optional [arg endtag] argument is specified, the content
 between the opening and closing tags is replaced by the
 [arg adpstring], and the enclosed content is discarded. This behavior
 differs from the other tag registering commands, where the enclosed
 content can be processed or passed to the handler.

[para] Note: Attributes specified in the tag are not accessible when
 using [cmd ns_adp_registeradp]. If you need to access attributes or
 the enclosed content, consider using [cmd ns_adp_registerproc] or
 [cmd ns_adp_registerscript].



[example_begin]
 # Register an ADP snippet to display the current date as tag "printdate"
 ns_adp_registeradp printdate {
      The current date is: <% ns_adp_puts [lb]ns_httptime [lb]ns_time[rb][rb] %>
 }
[example_end]

Usage example in an ADP page:

[example_begin]
 ... header ...
 <p>This is my page.</p>
  <printdate>
 ... footer ...
[example_end]


[call [cmd ns_adp_registerproc] \
	[arg tag] \
	[opt [arg endtag]] \
	[arg proc]]

 Registers a Tcl procedure to be evaluated when the given [arg tag] is
 encountered in ADP content.

 [para]
 This command is intended for positional and lightweight tag handlers.
 Only the value* of the attributes specified in the tag are passed to
 the procedure [arg proc]; attribute names are discarded. Attribute
 values are passed as positional arguments, in the order in which they
 appear in the tag.


 [para]
 If the optional [arg endtag] is specified, the procedure will receive one
 additional final argument containing the literal content enclosed
 between the opening and closing tags. The enclosed content is not
 evaluated and is passed unchanged as a single text block.

 [para] The procedure [arg proc] will be called
 with a variable number of arguments, one for each attribute value
 provided in the tag. If the [arg endtag] argument is specified, the
 procedure will also receive a final argument containing the content
 enclosed between the tags. No evaluation of the enclosed content is
 performed; it is passed as a single text block.

 [para]
 When the procedure is invoked, its return value replaces the tag (and the
 enclosed content, if [arg endtag] is specified) in the output.


[example_begin]
 # Define the tag handler
 proc geturltag {args} {
    # Extract the URL from the last argument
    set url [lb]lindex $args end[rb]
    # Extract any options (attributes)
    set options [lb]lrange $args 0 end-1[rb]
    # Perform an HTTP GET request
    set response [lb]ns_http run {*}$options $url[rb]
    # Return the response body
    dict get $response body
 }
 
 # Register the tag handler with an opening and closing tag
 ns_adp_registerproc geturl /geturl geturltag
[example_end]

Usage example in an ADP page:
[example_begin]
 ... header ...
 <p>This is my page.
 
 <geturl -timeout 3s >https://example.com/hello.html</geturl>
 
 <p>Next paragraph.</p>
 ... footer ...
[example_end]


[para]
 Note:

 [list_begin itemized]
 [item] Only attribute values are passed to the procedure; attribute names are not available.
 [item] Attribute values are passed positionally, in the order they appear in the tag.
 [item] This command is suitable when attribute names are irrelevant
 and a simple positional calling convention is sufficient.
 [list_end]

[example_begin]
 proc ::reporttags {args} {
   return $args
 }
 ns_adp_registerproc reporttags  ::reporttags
 
 ns_adp_parse -string {<reporttags CamelCase x=X A=a>}
 # Returns: CamelCase X a
[example_end]


[call [cmd ns_adp_registerscript] \
	[arg tag] \
	[opt [arg endtag]] \
	[arg proc]]

 Registers a Tcl procedure [arg proc] to be evaluated when the given
 [arg tag] is encountered in ADP content.

 [para]
 This command provides structured access to tag attributes and is
 intended for cases where attribute names and values must be
 distinguished. Attributes are passed to the procedure as an [cmd ns_set]
 containing name/value pairs.

 [para]
 If the tag is registered with an [arg endtag], the procedure is invoked
 with two arguments: first, the enclosed content as a string, and second,
 the [cmd ns_set] containing the tag attributes. When registered without
 an end tag, the procedure is called with a single argument (the
 [cmd ns_set] of attributes).

 [para]
 Attributes specified without an explicit value (no equals sign) are
 stored in the [cmd ns_set] with their name as the value.

[para]
When the tag is parsed, it is replaced by the result of the registered
[arg proc].

[para]

[example_begin]
 # Define the tag handler
 proc onday {string attributes} {
   # Get the 'day' attribute
   set day [lb]ns_set get -nocase $attributes day[rb]
   
   # Check if the current date matches the specified day
   if {[lb]ns_fmttime [lb]ns_time[rb] "%m/%d"[rb] eq $day} {
     return $string
   }
 }
 
 # Register the tag handler with an opening and closing tag #
 ns_adp_registerscript onday /onday onday
[example_end]


Usage example in an ADP page:

[example_begin]
 ... header ...
 <p>This is my page.</p>
 
 <onday day="12/25"><p>Merry Christmas and a Happy New Year</p></onday>
 
 <p>Next paragraph.</p>
 ... footer ...
[example_end]


[para]
Note:

[list_begin itemized]
[item] Attribute names and values are available via the [cmd ns_set] passed to the procedure.
[item] Attribute order is not significant.
[item] This command should be used when tag behavior depends on
       specific attribute names or optional flags.
[list_end]


[para]
The following is a simple way of handling conditional content in ADPs:



[example_begin]
 # Store content in a global variable
 proc remember {input tagset} {
   set tagname [lb]ns_set get -nocase $tagset name[rb]
   if {$tagname eq ""} {
     set ::_adp_memory($tagname) $input
     return ""
   } else {
     return $input
   }
 }
 
 # Retrieve and parse stored content
 proc recall {name} {
   if {[lb]info exists ::_adp_memory($name)[rb]} {
     set parsecommand [lb]list ns_adp_parse -string[rb]
     lappend parsecommand $::_adp_memory($name)
     ns_adp_puts -nonewline [lb]uplevel $parsecommand[rb]
   } else {
     ns_log Error "[lb]ns_adp_argv 0[rb]: Unable to recall"
   }
 }
[example_end]

If the preceding Tcl has been executed (perhaps during server startup),
then the following ADP fragment displays the results of a
database query in a table, or shows "No rows in
result." if there are no rows:

[example_begin]
 <%
  set rows {}
  set db [lb]ns_db gethandle[rb]
  ns_db exec "select somecolumn from sometable"
  set row [lb]ns_db bindargs $db[rb]
  while {[lb]ns_db getrow $db $row[rb]}  {
      lappend rows [lb]ns_set get $row "somecolumn"[rb]
  }
  ns_db releasehandle $db
 %>
 
 <remember name="has-rows_header"> <table> </remember>
 <remember name="hasrows_rows"> <tr><td><%=$column%></td></tr> </remember>
 <remember name="hasrows_footer"> </table> </remember>
 No rows in result.
 <remember name="norows">
 
 <%
  if {[lb]llength $rows[rb] > 0}  {
    recall "hasrows_header"
    foreach row $rows {
      set column $row
      recall "hasrows_rows"
    }
    recall "hasrows_footer"
  } else {
    recall "norows"
  }
 %>
[example_end]

 In this example, the `remember` procedure stores different parts of
 the output, and the `recall` procedure retrieves and processes them
 conditionally based on whether there are rows from the database
 query.

[list_end]

[see_also ns_register ns_adp ns_adp_include ns_set]
[keywords "server built-in" ADP tag "custom tags"]

[manpage_end]

