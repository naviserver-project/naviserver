[include version_include.man]
[manpage_begin ns_http n [vset version]]
[moddesc {NaviServer Built-in Commands}]

[titledesc {Simple HTTP client functionality}]

[description]

This command provides a simple HTTP and HTTPS client functionality.
It can be used to create, dispatch, wait on and/or cancel requests
and process replies from web servers.

[section {COMMANDS}]

[list_begin definitions]

[call [cmd "ns_http queue"] \
	[opt [option "-binary"]] \
	[opt [option "-body_size [arg size]"]] \
	[opt [option "-body [arg B]"]] \
	[opt [option "-body_file [arg fn]"]] \
	[opt [option "-body_chan [arg chan]"]] \
	[opt [option "-cafile [arg CA]"]] \
	[opt [option "-capath [arg CP]"]] \
	[opt [option "-cert [arg C]"]] \
	[opt [option "-raw"]] \
	[opt [option "-donecallback [arg call]"]] \
	[opt [option "-headers [arg ns_set]"]] \
	[opt [option "-hostname [arg HOSTNAME ]"]] \
	[opt [option "-keepalive [arg T]"]] \
	[opt [option "-keep_host_header"]] \
	[opt [option "-method [arg M]"]] \
	[opt [option "-spoolsize [arg int]"]] \
	[opt [option "-outputfile [arg fn]"]] \
	[opt [option "-outputchan [arg chan]"]] \
	[opt [option "-timeout [arg T]"]] \
	[opt [option "-expire [arg T]"]] \
        [opt [option "-proxy [arg dict]"]] \
	[opt [option "-verify"]] \
	[arg url] \
	]

[para]
The command [cmd "ns_http queue"] opens a connection to the web server
denoted in the [arg url] and returns (unless a [option -donecallback] is
specified) an [arg id], which might be used later in [cmd "ns_http wait"]
or [cmd "ns_http cancel"] to refer to this request. The command supports
both HTTP and HTTPS URIs.  The request is run in the default task queue
in a dedicated per-queue thread.

[list_begin options]

[opt_def -binary]
transmit the content in binary form (as a Tcl byte-array) no matter
what the content-type specifies.

[opt_def -body_size [arg size]]
specifies the expected size of the data which will be sent as the HTTP
request body. This option must be used when sending body data via Tcl
channels which are not capable of seeking. It is optional if sending
body data from memory or from a named file.

[opt_def -body [arg body]]
transmit the content of the passed string as the request body.
This option is mutually exclusive with [option -body_file] and [option -body_chan].
Implementation will try to guess the "Content-Type" of the body by checking
the type of the passed [option body].

[opt_def -body_file [arg fn]]
transmit the file specified via filename as the request body.
This option is mutually exclusive with [option -body] and [option -body_chan].
Implementation will try to guess the "Content-Type" of the body by checking
the extension of the passed [option fn].

[opt_def -body_chan [arg chan]]
transmit the content specified via Tcl channel, which must be opened for
reading, as the request body. The channel must be in blocking mode and
should be seekable (unless the [option -body_size] is specified).
This option is mutually exclusive with [option -body] and [option -body_file].
Caller should put "Content-Type" header in passed [option -headers] set
since the implementation cannot guess the correct value. If none found,
the "application/octet-stream" will be assumed.
For [cmd "ns_http queue"] command, the [option -body_chan] channel will be
dissociated from the current interpreter/thread and the ownership will be
transferred to the thread that runs the request. Upon [cmd "ns_http wait"]
the channel is tossed back to the running interpreter and can be manipulated.
It is the caller responsibility to close the channel when not needed any more.
The implementation will not do that (see [cmd "ns_http cleanup"] for exception).

[opt_def "-cafile" [arg CA]]
used for HTTPS URIs to specify the locations, at which CA
certificates for verification purposes are located. The certificates
available via [term cafile] and [term capath] are trusted.
The [term cafile] points to a file of CA certificates in PEM format.
The file can contain several CA certificates.

[opt_def "-capath" [arg CP]]
allows for HTTPS URIs to specify the locations, at which CA
certificates for verification purposes are located. [term capath] points to a
directory containing CA certificates in PEM format. The files each
contain one CA certificate. For more details, see
https://www.openssl.org/docs/manmaster/ssl/SSL_CTX_load_verify_locations.html

[opt_def "-cert" [arg C]]
used for HTTPS URIs to use the specified client certificate. The
certificates must be in PEM format and must be sorted starting with
the subject's certificate (actual client or server certificate),
followed by intermediate CA certificates if applicable, and ending at
the highest level (root) CA.

[opt_def -raw]
delivers the content as-is (unmodified), regardless of the content encoding.

[opt_def -donecallback [arg call]]
this callback argument will be executed as Tcl script when the requests
is completed.  The provided [arg call] is appended with two arguments,
a flag indicating a Tcl error or not (integer value 1 or 0), and the
dictionary as returned by [cmd "ns_http run"]. When this option is used,
[cmd "ns_http queue"] returns empty so user has no further control
(wait, cancel) on the task.

[opt_def -headers [arg ns_set]]
headers is the ns_set ID containing the additional headers to include
in the request.

[opt_def -hostname [arg HOSTNAME]]
used for HTTPS URIs to specify the hostname for the server
certificate. This option has to be used, when the host supports
virtual hosting, is configured with multiple certificates and supports
the SNI (Server Name Indication) extension of TLS.

[opt_def -keepalive [arg T]] when specified, set the keepalive value
of the connection to the specified time duration. The time can be
specified in any supported ns_time format.

[opt_def -keep_host_header]
allows the Host: header field for the request to be passed in via
the [option -headers] arg, otherwise it is overwritten.

[opt_def -method [arg method]]
Standard HTTP/HTTPS request method such as GET, POST, HEAD, PUT etc.

[opt_def -spoolsize [arg int]]
In case the result is larger than given value, it will be spooled to a
temporary file, a named file (see [option -outputfile]) or the Tcl
channel (see [option -outputchan]).
The value can be specified in memory units (kB, MB, GB, KiB, MiB, GiB).

[opt_def -outputfile [arg fn]]
receive the response content into the specified filename.
This option is mutually exclusive with [option -outputchan].

[opt_def -outputchan [arg chan]]
receive the response content into the specified Tcl channel, which must be
opened for writing. The channel must be in blocking mode.
This option is mutually exclusive with [option -outputfile].
For [cmd "ns_http queue"] command, the [option -outputchan] channel will be
dissociated from the current interpreter/thread and the ownership will be
transferred to the thread that runs the request. Upon [cmd "ns_http wait"]
the channel is tossed back to the running interpreter and can be manipulated.
It is the caller responsibility to close the channel when not needed any more.
The implementation will not do that (see [cmd "ns_http cleanup"] for exception).

[opt_def -expire [arg T]]
time to wait for the whole request to complete. Upon expiry of this timer,
request processing is unconditionally stopped, regardless of whether the
connection or some data to read/write is still pending. The time can be
specified in any supported ns_time format.

[opt_def -timeout [arg T]]
time to wait for connection setup and socket readable/writable state.
The time can be specified in any supported ns_time format. When a
domain name is resolved against several IP addresses, the provided
timeout span is used for every IP address. The default timeout is 5s.

[opt_def -verify]
used for HTTPS URIs to specify that the server certificate should be
verified. If the verification process fails, the TLS/SSL handshake is
immediately terminated with an alert message containing the reason for
the verification failure. If no server certificate is sent, because an
anonymous cipher is used, this option is ignored.

[opt_def -proxy [arg dict]]
Controls whether to handle HTTP/HTTPS requests over an intermediate proxy.
The argument must be a valid Tcl dictionary with (at least) following
keys: [term host], [term port]. Optionally, a [term tunnel] boolean key
may be specified.
The [term host] must hold the hostname or IP address of the proxy
server. The [term port] the TCP port of the proxy server. If [term host]
is omitted, no other keys from the dictionary are evaluated and the proxy
connection is suppressed; the request is handled as if the option was
not specified.  If, however, [term host] is specified, it will require
the presence of the [term port], otherwise an error is thrown.
The optional [term tunnel] controls when to use HTTP-tunnel facility.
Without it, or if set to false, the HTTP connections are handled over
the caching-proxy and HTTPS connections over the HTTP-tunnel.
With the [term tunnel] set to true, the HTTP-tunneling is used for both
HTTP and HTTPS connections.
Currently, no proxy authentication is supported. This will be added later.

[list_end]

[call [cmd "ns_http run"] \
	[opt [option "-binary"]] \
	[opt [option "-body_size [arg size]"]] \
	[opt [option "-body [arg B]"]] \
	[opt [option "-body_file [arg fn]"]] \
	[opt [option "-body_chan [arg chan]"]] \
	[opt [option "-cafile [arg CA]"]] \
	[opt [option "-capath [arg CP]"]] \
	[opt [option "-cert [arg C]"]] \
	[opt [option "-raw"]] \
	[opt [option "-donecallback [arg call]"]] \
	[opt [option "-headers [arg ns_set]"]] \
	[opt [option "-hostname [arg HOSTNAME ]"]] \
	[opt [option "-keepalive [arg T]"]] \
	[opt [option "-keep_host_header"]] \
	[opt [option "-method [arg M]"]] \
	[opt [option "-spoolsize [arg int]"]] \
	[opt [option "-outputfile [arg fn]"]] \
	[opt [option "-outputchan [arg chan]"]] \
	[opt [option "-timeout [arg T]"]] \
	[opt [option "-expire [arg T]"]] \
	[opt [option "-verify"]] \
	[arg url] \
	]

[para]
Send an HTTP request and wait for the result.  The command [cmd "ns_http run"]
is similar to [cmd "ns_http queue"] followed by [cmd "ns_http wait"].
The http request is run in the same thread as the caller.

[call [cmd "ns_http wait"] \
	[opt [option "-timeout [arg T]"]] \
	[arg id] \
  ]

[para]
Waits for the queued command specified by the [term id] returned from
[cmd "ns_http queue"] to complete. Returns a Tcl dictionary with the
following keys: [term status], [term time], [term headers].
The [term status] contains HTTP/HTTPS status code (200, 201, 400, etc).
The [term time] contains elapsed request ns_time.
The [term headers] contains name of the set with response headers.
If the request body was given over a Tcl channel, it will add key
[term body_chan] in the passed result dictionary.
If the response was not spooled in the file nor channel, it will return
[term body] key with the response data. Otherwise it will return either
[term file] if the response was spooled into the named (or temporary) file
or [term outputchan] if the request was spooled into a Tcl channel.
For HTTPS requests it will add [term https] with some low-level TLS
parameters in a Tcl dictionary format.

The specified [option -timeout] specified the maximum duration of
the request to complete. The time can be specified
in any supported ns_time format.

[para]
On success [cmd "ns_http wait"] returns the same dictionary as the
[cmd "ns_http run"]. On error, leaves descriptive error message
in the interpreter result. On timeout, sets the Tcl [variable ::errorCode]
variable to NS_TIMEOUT in addition to leaving the error message.

[list_begin arguments]
[arg_def "" id]
ID of the HTTP request to wait for.
[list_end]

[call [cmd "ns_http cancel"] [arg id]]

Cancel queued HTTP/HTTPS request by the ID (of the request)
as returned by [cmd "ns_http queue"] command. The command returns
empty result. The sense behind this command is to be able to
terminate runaway requests or requests that have been timed out.
Even completed requests can be cancelled if nobody is interested
in the request result.

[list_begin arguments]
[arg_def "" id]
ID of the HTTP request to cancel.
[list_end]

[call [cmd "ns_http cleanup"]]

Cancel all pending HTTP/HTTPS requests issued in the current
interpreter. At this point, any Tcl channels that have been
optionally assigned to a task, will be automatically closed.

[call [cmd "ns_http list"] ?[arg id]?]

If [arg id] was specified, returns a single list in form
of: "id url status".
If [arg id] was not specified, returns a list of N elements
where each element is in itself a list in form of: "id url status".
The status can be one of: "done", "running", "error".

[list_begin arguments]
[arg_def "" id]
Optional ID of the HTTP request to list.
[list_end]

[call [cmd "ns_http stats"] ?[arg id]?]

Returns statistics from the currently running request in the form of
a list of Tcl dictionares.  If optional [arg id] was specified, just
one dictionary containing details about the requested task will be
returned, or empty if the task cannot be found. Otherwise a list of
dictionaries will be returned.
One returned dictionary contains following keys:
[term task], [term url], [term requestlength], [term replylength],
[term sent], [term received], [term sendbodysize], [term replybodysize],
[term replysize].
The [term task] returns the ID of the Http task.
The [term url] returns the URL for the given task.
The [term requestlength] returns the length of the complete http request,
including header line, all of the headers plus optional request body.
The [term replylength] returns the value of the Content-Length as
returned by the remote. This can be zero if the length of returned data
is not known in advance.
The [term sent] returns number of bytes sent to the remote. This includes
the header line, all of the headers plus optional request body.
The [term received] returns number of bytes received from the remote. This
includes status line, all of the headers plus the optional reply body.
The [term sendbodysize] returns number of bytes of the request body sent
to the remote so far.
The [term replybodysize] returns number of bvtes of the reply body received
from the remote so far.
The [term replysize] returns number of bytes of the body received from the
remote so far. Difference to the [term replybodysize] is that this element
tracks number of body bytes prior optional deflate step for compressed contents,
whereas the [term replybodysize] tracks the number of body bytes of the
deflated contents.  For uncompressed reply content, both [term replysize]
and [term replybodysize] will have the same value.

[list_begin arguments]
[arg_def "" id]
Optional ID of the HTTP request to get statistics for.
[list_end]
[list_end]

[section EXAMPLES]

First, a minimal GET example:
[example_begin]
 % ns_http queue http://www.google.com
 http0

 % ns_http wait http0
 status 302 time 0:97095 headers d0 body { ... }
[example_end]

[para]
The second example is a code snippet making a requests via HTTPS
(note that HTTPS is supported only if the NaviServer was compiled
with OpenSSL support.

[example_begin]
 % set result [lb]ns_http run https://www.google.com[rb]
 % dict get $result status
 302
[example_end]

If the returned data is too large to be retained in memory
you can use the [option -spoolsize] to control when the
content should be spooled to file. The spooled filename
is contained in the resulting dict under the key [term file].

[example_begin]
 % set result [lb]ns_http run -spoolsize 1kB https://www.google.com[rb]
 % dict get $result file
 /tmp/http.83Rfc5
[example_end]

For connecting to a server with virtual hosting that provides
multiple certificates via SNI (Server Name Indication) the
option [option -hostname] is required.

[para]

The third example is a code snippet making a POST requests via
HTTPS and provides url-encoded POST data. The examples sets a
larger timeout on the request, provides requests headers and
returns reply-headers.

[example_begin]
 #######################
 # construct POST data
 #######################
 set post_data [lb]join [lb]lmap {key value} {
     q NaviServer
 } {
   set _ "[lb]ns_urlencode $key[rb]=[lb]ns_urlencode $value[rb]"
 }[rb] &[rb]
 
 #######################
 # submit POST request
 #######################
 set requestHeaders [lb]ns_set create headers "Content-type" "application/x-www-form-urlencoded"[rb]
 
 set r [lb]ns_http queue -method POST \
   -headers $requestHeaders \
   -timeout 10.0 \
   -body $post_data \
   https://httpbin.org/anything[rb]
 
 #######################
 # output results
 #######################
 ns_log notice "status [lb]dict get $r status[rb]"
 ns_log notice "reply [lb]dict get $r [rb]"
 ns_log notice "headers [lb]dict get $r headers[rb]"
[example_end]

The forth example is a code snippet that sets a larger timeout on the
request, provides an ns_set for the reply headers, and spools
results to a file if the result is larger than 1000 bytes.

[example_begin]
 set requestHeaders [lb]ns_set create headers Host localhost[rb]
 
 set h [lb]ns_http queue -timeout 10.0 http://www.google.com[rb]
 ns_http wait -result R -headers $requestHeaders -status S -spoolsize 1kB -file F $h
 
 if {[lb]info exists F[rb]} {
   ns_log notice "Spooled [lb]file size $F[rb] bytes to $F"
   file delete -- $F
 } else {
   ns_log notice "Got [lb]string length $R[rb] bytes"
 }
[example_end]

Example for downloading a file from the web into a named file
or passed Tcl channel. Note the [option -spoolsize] of zero
which will redirect all received data into the file/channel.
Without the [option -spoolsize] set, all the data would be
otherwise stored in memory.

[example_begin]
 % ns_http run -outputfile /tmp/reply.html -spoolsize 0 http://www.google.com
 status 302 time 0:132577 headers d2 file /tmp/reply.html
[example_end]

[example_begin]
 % set chan [lb]open /tmp/file.dat w[rb]
 % ns_http run -outputchan $chan -spoolsize 0 http://www.google.com
 status 302 time 0:132577 headers d2 outputchan file22
 
 % close $chan
[example_end]
[see_also ns_httptime ns_set ns_urlencode]
[keywords "global built-in" http-client HTTP HTTPS POST GET nssock spooling SNI]

[manpage_end]
