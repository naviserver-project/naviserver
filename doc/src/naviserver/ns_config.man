[include version_include.man]
[include version_include.man]

[manpage_begin ns_config n [vset version]]
[moddesc   {NaviServer Built-In Commands}]
[titledesc {Configuration parameters}]

[description]
The NaviServer process reads a Tcl configuration file (specified with the
[emph -t] command line option) during early start-up. After it has changed to the
correct [term "user id"] and [term "group id"], bound to port 80, and possibly
performed a [term chroot], it evaluates the configuration file as a Tcl script.

[para]
The configuration file may contain standard Tcl and NaviServer commands, plus
the [cmd ns_section] and [cmd ns_param] commands used to define a configuration
structure. Parameters are key-value pairs and are grouped into sections. Section
names must be unique -- parameter keys may be duplicates.

[para]
The configuration is global and read-only. Parameters may be retrieved at
run-time using [cmd ns_config], although usually configuration parameters are
used by Tcl library files at start-up.




[section {COMMANDS}]

The following commands are available at run-time to retrieve information from the
configuration file.


[list_begin definitions]

[call [cmd ns_config] \
      [opt [option -all]] \
      [opt [option -bool]] \
      [opt [option -int]] \
      [opt [option "-min [arg integer]"] ] \
      [opt [option "-max [arg integer]"] ] \
      [opt [option -exact] ] \
      [opt [option -set] ] \
      [opt --] \
      [arg section] \
      [arg key] \
      [opt [arg default] ] ]

Returns the parameter value associated with the given [arg section] and [arg key] from
the configuration file.  If the parameter is not found, then the optional [arg default]
is returned, otherwise the empty string is returned.

[para]
Parameter keys are matched case-insensitively. The first parameter is returned
if duplicate keys are present.

[list_begin options]

[opt_def -all]
Specifies that the command should return all values provided
from the underlying multi-set. If not specified, just the first value
is returned. The command is similar to [cmd "ns_set get"].

[opt_def -bool]
Specifies that the parameter should be a valid boolean value, using any form
understood by [cmd "string is boolean"]. An error will be thrown if this is not
the case, unless a valid [arg default] is given.

[opt_def -int]
Specifies that the parameter should be a valid integer value, using any form
understood by [cmd "string is integer"]. An error will be thrown is this is not
the case, unless a valid [arg default] is given.

[opt_def -min minint]
Specifies the lower bound for an integer parameter value. An error will be thrown
if the parameter < [arg minint], unless a valid default value is given.

[opt_def -max maxint]
Specifies the upper bound for an integer parameter value. An error will be thrown
if the parameter > [arg maxint], unless a valid default value is given.

[opt_def -exact]
Specifies case-sensitive parameter key matching. Not recommended.

[opt_def -set]
Specifies that the parameter is set to the default value,
when this parameter was not provided earlier. When this is done,
the default is later retrievable via [cmd ns_config].

[list_end]




[call [cmd ns_configsection] \
      [opt [option "-filter unread|defaulted|defaults"] ] \
      [opt --] \
      [arg section] ]

Returns the [cmd ns_set] which contains the actual values for all
parameters defined in the specified [arg section].  If there is no
matching section, an empty string is returned.

[para] The [option "-filter"] can be used to return different kind of
information about the parameters defined in this section.

[list_begin options]

[opt_def -filter [term unread]] return the parameter, which were set
during configuration (i.e., in the configuration file) but which were
not read in the startup phase of the server. This option is useful to
determine e.g. typographical errors of specified parameter names.

[opt_def -filter [term defaulted]] return the parameter, from which
the default values were read (i.e., which were not explicitly set)

[opt_def -filter [term defaults]] return the defaults of the
parameter. This is useful for contrasting the actual values with the
default values, e.g. in a web based interface.

[list_end]


[call [cmd ns_configsections] ]

Returns a list of [cmd ns_set]s, one for every section in the configuration
file.  The sets contain the key-value pairs for the configuration section that
the set represents. The [cmd ns_set] name contains the [arg section].



[list_end]



[section {CONFIGURATION FILE COMMANDS}]

 The following commands are available only within the Tcl
 configuration file, which is evaluated once at server start-up.

[list_begin definitions]

[call [cmd ns_section] \
      [opt [option -update]] \
      [opt --] \
      [arg name]]

 Begins a new configuration section identified by the specified
 [arg name]. All subsequent calls to [cmd ns_param] will add parameters to
 this section until another [cmd ns_section] command is invoked with a
 different section name.


[para]

 Multiple invocations of [cmd ns_section] using the same [arg name]
 allow you to build the section incrementally. The following two
 snippets are equivalent:

[para]

Snippet 1:
[example_begin]
 [cmd ns_section] foo {
   [cmd ns_param] x 1
   [cmd ns_param] y 1
 }
[example_end]

Snippet 2:
[example_begin]
 [cmd ns_section] foo {
   [cmd ns_param] x 1
 }
 [cmd ns_section] foo {
   [cmd ns_param] y 1
 }
[example_end]

[para]

 All configuration data is stored internally in [cmd ns_set]
 structures. Since ns_set is defined as a multi-map storage, multiple
 values can be associated with the same key. This powerful feature may
 lead to surprises: when a scalar value is requested and multiple
 values are stored for the same key, only the first value is returned
 (as with [cmd "ns_set get"]). To retrieve all values for a key, use
 the [option -all] option with [cmd ns_config] or [cmd ns_set].

[para]

 The [option -update] flag can be used with [cmd ns_section] to update
 an existing section by overwriting previous values instead of simply
 appending new ones.

[example_begin]
 [cmd ns_section] foo {
   [cmd ns_param] x 1
 }
 
 # Later in the configuration file:
 [cmd ns_section] -update foo {
   [cmd ns_param] x 2
 }
 
 # Query the value of parameter 'x'
 [cmd ns_config] foo x
 # Result: 2
[example_end]


[call [cmd ns_param] \
      [arg key] \
      [arg value]]

 Sets the specified [arg key] to [arg value] in  currently active
 section. Keys are matched in a case-insensitive manner, and duplicate
 keys are allowed.



[call [cmd ns_configure_variables] \
      [opt [option "-configRoot [arg directory]"]] \
      [opt [option "-silent [arg boolean]"]] \
      [opt --] \
      [arg prefix] \
      [arg defaultConfig] ]

Turns the keys from the dict [arg defaultConfig] into Tcl variables in the
caller's scope (the configuration file).  Values are determined per key
using the following precedence:

[list_begin enumerated]
[enum] A variable already set in the configuration file (highest precedence).
[enum] An environment variable named [const {${prefix}$key}].
[enum] The value from the dict [arg defaultConfig] (lowest precedence).
[list_end]


[para]
Values are processed via [cmd subst] to allow references to previously
defined variables such as [const {$home}] inside dict values.

[para]
When the key [const setupfile] exists in [arg defaultConfig] and resolves
to a non-empty value, the referenced Tcl file is sourced after the variable
assignment pass.  This setup file is intended for instance-specific
variable assignments (typically without [cmd ns_section] blocks).  A relative
setupfile path is resolved against a configuration root directory (see
[option -configRoot]).

[list_begin options]

[opt_def "-configRoot directory"]
Specifies the base directory for resolving a relative [const setupfile].
When not provided (or empty), the configuration root is derived from
[cmd "ns_info config"] (the value passed to [option -t]): if [option -t] is a
directory, that directory is used; if [option -t] is a file, its directory
is used.  If [cmd "ns_info config"] is empty, the current working directory
([cmd pwd]) is used.

[opt_def "-silent boolean"]
Controls logging of variable assignments.  When true, [cmd ns_configure_variables]
does not emit the usual [cmd ns_log] notices about where values come from
(defaults, environment variables, config file, or setup file).  The default
is false.

[list_end]
[list_end]

[para] This command is typically used near the beginning of a
 configuration file to establish all configuration variables before
 [cmd ns_section] blocks are evaluated.

[example_begin]
 set defaultConfig {
   home        /var/www
   logdir      {$home/logs}
   httpport    8080
   httpsport   ""
   setupfile   ""
 }
 
 # Allow overrides via environment variables with prefix "oacs_"
 ns_configure_variables "oacs_" $defaultConfig
[example_end]

[para] With this configuration, the following invocation overrides the
 HTTP port and sources an instance-specific setup file:

[example_begin]
 oacs_httpport=8081 oacs_setupfile=instance.tcl nsd -t conf/openacs-config.d -f
[example_end]

[para] Notes:

[list_begin itemized]

[item] The command reports the origin of each variable value (default,
 environment variable, configuration file, or setup file) via the
 system log unless [option "-silent"] is used.

[item] Only scalar variables are processed; array variables are
 ignored.

[item] This command is a core part of the shipped configuration
 framework and is used by the sample configurations provided with
 NaviServer.


[list_end]


[section EXAMPLES]

 The following example demonstrates how to set parameter values for
 the [const foo] module:

[example_begin]
 [cmd ns_section] "ns/server/server1/modules/foo" {
   [cmd ns_param]   enabled   true
   [cmd ns_param]   map       /some/url
   [cmd ns_param]   map       /some-other/url
 }
[example_end]

 Alternatively, the section content can be specified without braces
 (old style):

[example_begin]
 [cmd ns_section] "ns/server/server1/modules/foo"
 [cmd ns_param]   enabled   true
 [cmd ns_param]   map       /some/url
 [cmd ns_param]   map       /some-other/url
[example_end]

 The following example shows how to read configuration parameters for
 the [const foo] module.  In this case, the [cmd ns_config] command
 checks for a boolean [const enabled] parameter (defaulting to
 false). If enabled, it retrieves all values associated with the
 [const map] key and registers a handler for GET requests on each
 corresponding URL:

[example_begin]
 set path ns/server/[lb][cmd ns_info] server[rb]/modules/foo
 
 if {[lb][cmd ns_config] -bool $path enabled false[rb]} {
    foreach url [lb][cmd ns_config] -all $path map[rb] {
      [cmd ns_register_proc] GET $url foo_module_handler
    }
 }
[example_end]

 The following example prints all configuration parameters from every
 section of the configuration file. It retrieves each section's ns_set
 via [cmd ns_configsections] and uses [cmd "ns_set array"] to list all
 key/value pairs:

[example_begin]
 [cmd ns_register_proc] GET /config-print {
    set config ""
    foreach section [lb][cmd ns_configsections][rb] {
        append config "section: [lb][cmd ns_set] name $section[rb]\n"
        foreach {key value} [lb][cmd ns_set] array $section[rb] {
            append config "  $key: $value\n"
        }
    }
    [cmd ns_return] 200 text/plain $config
 }
[example_end]




[see_also ns_set ns_env ns_info ns_register]
[keywords "global built-in" configuration parameter interp startup]
[manpage_end]
