[include version_include.man]
[manpage_begin {tcl-overview} n [vset version]]
[moddesc {NaviServer Manual}]

[titledesc {NaviServer Tcl Overview}]

[description]

This chapter contains general information about using Tcl whether you
are using the ADP approach or the Tcl Libraries approach, both of
which use the same features of Tcl.

[section {What is Tcl?}]

Tcl is a programming system developed by John Ousterhout at the
University of California, Berkeley. According to Dr. Ousterhout:


[para]

Tcl is a simple scripting language for controlling and extending
applications; its name stands for "tool command language". Tcl
provides generic programming facilities, such as variables and loops
and procedures, that are useful in a variety of applications.
Furthermore, Tcl is embeddable. Its interpreter is a library of C
procedures that can easily be incorporated into applications, and each
application can extend the core Tcl features with additional commands
for that application.

[para]


NaviServer supports the Tcl 8.* commands. When you write NaviServer
extensions in Tcl, you use the core functionality of Tcl as well as a
set of NaviServer-specific Tcl functions. For example, there is a NaviServer Tcl function called ns_conn that allows your Tcl script to
obtain information about the current connection. In addition,
functions are available for returning results to Web clients,
accessing databases, and managing the permissions (access control)
system.


[para]


This book describes the two different methods for extending NaviServer
with Tcl, using NaviServer Dynamic Pages (Chapter 2) and Tcl libraries
(see Chapter 3), plus general information on using Tcl with either
method (see Chapter 4). It also provides a reference for NaviServer's
Tcl API (see Chapter 5).


[para]


 [subsection {Recommended Reading}]


[para]


For more information on the Tcl language, we recommend the following
sources:


[para]


[list_begin itemized]

[item] Practical Programming in Tcl and Tk, by Brent B. Welch (Prentice Hall PTR, 1995)


[para]


[item] Tcl and the Tk Toolkit, by John K. Ousterhout (Addison-Wesley, 1994)


[para]


[item] For the complete manual pages for the Tcl v7.6 commands, visit the
     web site http://www.scriptics.com/man.


[para]


[item] Philip Greenspun's web site, http://www-swiss.ai.mit.edu/wtr/dead-trees/, which includes
     chapters from his book, Database Backed Web Sites, The Thinking Person's Guide to Web Publishing,
     and numerous examples of NaviServer Tcl database applications.


[para]


[item] The web site: http://www.yahoo.com/Computers_and_Internet/Programming_Languages/Tcl_Tk


[list_end]



[para]


[section {Why Use Tcl?}]


[para]


You can extend NaviServer using Tcl, C, or CGI. However, using Tcl has
the following advantages:


[para]

[list_begin itemized]

[item] Creating new Tcl scripts is very easy. You can embed Tcl scripts
 directly into HTML pages using NaviServer Dynamic Pages (ADPs), or
 you can create .tcl files and register them to handle URLs or URL
 hierarchies.


[para]


[item] Tcl's scripting language is easier to learn and use than compiled
 programming languages. It provides general programming
 capabilities, such as variables, loops, and procedures, but it
 doesn't require strong type definitions or compilation. Plus,
 NaviServer's Tcl interface provides a complete set of Tcl
 extensions specifically oriented towards web server applications,
 such as returning content to the client, accessing form data,
 accessing databases, and logging.


[para]


[item] Many of the basic NaviServer services are implemented as Tcl
 scripts. In fact, most of the database operations such as handling
 database inserts, queries, and updates, are written as Tcl
 scripts. Not only has the Tcl interface greatly reduced the
 development time for the NaviServer engineering team, it also
 allows you to easily modify the basic database operations by
 simply editing the Tcl scripts that implement the functionality.


[para]


[item] The Tcl and C interfaces typically provide better performance than
 CGI. However, if you have existing CGI programs, you may want to
 use NaviServer's CGI interfaces to take advantage of
 previously-existing code.


[list_end]



[para]


The C interface for NaviServer is described in the NaviServer C
Developer's Guide, and the CGI interface is described in the NaviServer
Administrator's Guide.


[para]



[section {ADPs and Tcl Libraries}]


[para]


There are two ways to extend NaviServer using Tcl, and each is
better-suited to different situations:


[para]


[list_begin itemized]

[item] NaviServer Dynamic Pages (ADPs): ADPs allow you to embed Tcl
 scripts directly into HTML pages. The script(s) are interpreted
 dynamically when the page is accessed. ADPs are ideal in
 situations where you want to generate all or part of a specific
 page dynamically. You can re-use code by storing Tcl scripts in
 Tcl libraries and invoking them from within multiple ADPs. You can
 also include files and parse other ADPs from within your ADPs.


[item] Tcl Libraries: The alternative to embedding Tcl scripts in HTML
 pages using ADPs, is to store Tcl scripts in Tcl libraries. You
 can define scripts that can be called from ADPs, schedule
 procedures to run at certain times, register scripts to handle
 specific URLs or URL hierarchies, register scripts to handle all
 URLs with a specific file extension, and register filters to be
 run in addition to a URL's registered procedure.


[list_end]


[section {Tcl Interpreters}]

   During NaviServer initialization, only one interpreter
   exists. While modules are loaded and initialized, they may add
   procedures to the interpreter. When initialization is complete (all
   modules are loaded and all Tcl libraries have been executed),
   NaviServer creates from this a blueprint consisting of the
   preloaded modules (mostly Tcl files).
   This blueprint is provided later to every thread (e.g. connection
   threads, or background threads) upon initialization.


[para][image blueprint3 "NaviServer Blueprint"]
[para]Figure 1: NaviServer Blueprint (per server)

[para]

   Blueprints are used to reduce latency. When e.g. a connection
   thread processes an incoming request, all the basic Tcl code is
   already available and can be used by the interpreter without
   further loading operations.

[para]

   In order to update the blueprints, NaviServer provides built-in
   support for updating via the command [cmd ns_eval] or via the lower
   level command [cmd ns_ictl].

[section {Global Variables}]

   The NaviServer blueprint contains Tcl functions, XOTcl/NX classes
   and objects and also namespaced variables. This are all Tcl
   variables, which are not in the topmost namespace "::". The
   top-level variables are treated as "scratch variables", which are
   flushed automatically after every request. The global variables are
   often used for providing values for templating; cleaning these
   automatically can avoid unexpected interactions.

[para]

   In addition of the per-interpreter variables, NaviServer provides
   shared variables, which can be accessed via the nsv_* interface
   (see below for an example). When such a variable is altered, its
   value is immediately available in all threads.


[section {Sharing Files Between Interpreters}]


   To share variables between the interpreters in a group, use the Tcl
   nsv_* command.
   
[para]

   For example:


[example_begin]
    #init time:
    nsv_set . sharedFile [lb]open myfile.dat[rb]
[example_end]



[para]
   You must manually close the shared file (by performing a close) when
   it is no longer needed or when the server is shut down.


[para]
   Note that there is no implicit locking between interpreters. As a consequence
   one interpreter can be performing a [cmd gets] command on the
   shared file at the same time another interpreter performs a [cmd close] on
   the same file. The results are unpredictable, and could potentially
   cause a core dump. To avoid this situation, use a mutex to protect
   access to the file whenever it is accessed.


[para]
   For example:, at initialization time (for example, in your init.tcl
   script), open a shared file and create a mutex:


[para]

[example_begin]
    nsv_set . sharedLock [lb]ns_mutex create[rb]
    nsv_set . sharedFile [lb]open myfile.dat[rb]
[example_end]



[para]


   At run time, use the mutex to protect any actions you perform on the
   shared file:

[para]

[example_begin]
    ns_mutex lock [lb]nsv_get . sharedLock[rb]
    set sharedFile  [lb]nsv_get . sharedFile[rb]
    puts $sharedFile ...
    gets $sharedFile ...
    ns_mutex unlock [lb]nsv_get . sharedLock[rb]
[example_end]



[para]
   At shutdown (for example, in your shutdown procedure registered with
   ns_atshutdown), close the file and destroy the lock:
[para]



[example_begin]
    close [lb]nsv_get . sharedFile[rb]
    ns_mutex destroy [lb]nsv_get . sharedLock[rb]
[example_end]



[para]


[section {Working with ns_set and Form Data}]


[para]


   An NaviServer operation procedure often manipulates sets of key-value
   pairs of string data, for example:


[para]


[list_begin itemized]

[item]rows of data from the database
[item]HTTP header information
[item]HTML form data


[list_end]



[para]


    [subsection {ns_set Data Structure}]


[para]


   In the NaviServer C API these data are manipulated using the ns_set
   data structure. In the Tcl API the ns_set command can be used to
   manipulate underlying ns_set data structures that are generated by the
   NaviServer communications layer or the database services module.


[para]


   The example below shows a typical use of the ns_set Tcl command to
   manipulate ns_set structures.


[example_begin]
   # Example 2: Show header data
    #
    # Things to notice:
    # * The same function is registered for two different URLs
    # with different context.
    #
    # * The headers are pulled out of the conn using the
    # ns_conn function.
    #
    # * The value for a particular header line is extracted
    # with "ns_set iget" (case insensitive get).
 
    ns_register_proc GET /showbrowser showheader User-Agent
    ns_register_proc GET /showhost    showheader Host
 
    proc showheader {key} {
          set value [lb]ns_set iget [lb]ns_conn headers[rb] $key[rb]
          ns_return 200 text/plain "$key: $value"
    }
[example_end]



[para]



[section {Tcl Examples}]


[subsection {Example 1: hello}]

The following example script implements a simple request procedure
which returns 'Hello World'.


[example_begin]
 # Example 1: Hello World
 #
 # This simple operation just returns a plain text message.
 #
 # Things to notice:
 #
 # * ns_register_proc takes as arguments:
 #   * the HTTP method
 #   * the URL that the procedure handles
 #   * the procedure that is executed
 #
 # * ns_return takes as arguments:
 #   * the passed in connection
 #   * a return status, in this case 200 for success
 #   * a MIME type
 #   * the actual string to return
 #
 # * ns_return properly formats the HTTP response for you.
 
 ns_register_proc GET /example/hello hello
 
 proc hello {} {
   ns_return 200 text/plain "Hello World"
 }
[example_end]

The same script could also be implemented by specifying the script
body directly in the [cmd ns_register_proc] command invocation.

[example_begin]
 ns_register_proc GET /example/hello {
   ns_return 200 text/plain "Hello World"
 }
[example_end]

The second variant is shorter, by defining procs, it might be easier
for complex examples to e.g. update the procs at runtime, etc.

[subsection {Example 2: showhdrs}]

The following example script shows how to access the HTTP headers sent
by the client within a Tcl script.


[example_begin]
 # Example 2: Show header data
 #
 # Things to notice:
 #
 # * The same function is registered for two different URLs
 #   with different context.
 #
 # * The headers are pulled out of the conn using the
 #   ns_conn function.
 #
 # * The value for a particular header line is extracted
 #   with "ns_set iget", the case insensitive counterpart to
 #   "ns_set get".
 
 ns_register_proc GET /showbrowser showheader User-Agent
 ns_register_proc GET /showhost    showheader Host
 
 proc showheader {key} {
    set value [lb]ns_set iget [lb]ns_conn headers[rb] $key[rb]
    ns_return 200 text/plain "$key: $value"
 }
[example_end]



[subsection {Example 3a: genstory}]

The following example script provides two request procedures. The
first procedure returns an HTML page for collecting a few fields of
data from the user. The second procedure utilizes the data to generate
a short story.



[example_begin]
 # Example 3a: Form generation and handling
 #
 # Two functions are registered. One generates and
 # returns an HTML form, and the other processes
 # the data in the form.
 #
 # Things to notice:
 #
 # * Different functions are registered to the same
 #   URL with different methods.  Note that some browsers
 #   do not cache results properly when you do this.
 #
 # * The genstory function returns an error status
 #   (500) if the client doesn't pass in any form data.
 #
 # * Form data is stored in an ns_set, and accessed
 #   like any other set (e.g., header data).
 #
 # * A counter is used to loop through all the key
 #   value pairs in the form.
 
 ns_register_proc GET  /example/genstory genstoryform
 ns_register_proc POST /example/genstory genstory
 
 proc genstoryform {} {
  ns_return 200 text/html \
 "<html>
 <head>
 <title>Automatic Story Generator</title>
 </head>
 <body>
 <h1>Automatic Story Generator</h1>
 <form action='genstory' method='POST'>
 Noun: <input type='text' name='noun1'><br>
 Noun: <input type='text' name='noun2'><br>
 Name: <input type='text' name='name1'><br>
 Name: <input type='text' name='name2'><br>
 Adjective: <input type='text' name='adjective1'><br>
 Adjective: <input type='text' name='adjective2'><br>
 Verb: <input type='text' name='verb1'><br>
 Verb: <input type='text' name='verb2'><br>
 <p><input type='submit' value='Generate'>
 </form>
 </body></html>
 "}
 
 proc genstory {} {
  set formdata [lb]ns_conn form[rb]
 
  if {$formdata eq ""} {
    ns_return 200 text/plain "Need form data!"
    return
  }
 
  # Build up a human-readable representation of the form data.
 
  set hrformdata "<dl>"
  set size [lb]ns_set size $formdata[rb]
  for {set i 0} {$i < $size} {incr i} {
    append hrformdata "<dt>[lb]ns_set key $formdata $i[rb]</dt><dd>[lb]ns_set value $formdata $i[rb]</dd>"
  }
  append hrformdata "</dl>"
 
  ns_return 200 text/html \
 "<html>
 <head>
 <title>The story of [lb]ns_set get $formdata name1[rb] and
 [lb]ns_set get $formdata name2[rb]</title>
 </head>
 <body>
 <h1>The story of [lb]ns_set get $formdata name1[rb] and [lb]ns_set get $formdata name2[rb]</h1>
 <p>Once upon a time [lb]ns_set get $formdata name1[rb] and
 [lb]ns_set get $formdata name2[rb] went for a
 walk in the woods looking for a [lb]ns_set get $formdata noun1[rb].
 [lb]ns_set get $formdata name1[rb] was
 feeling [lb]ns_set get $formdata adjective1[rb] because
 [lb]ns_set get $formdata name2[rb] was so
 [lb]ns_set get $formdata adjective2[rb].  So
 [lb]ns_set get $formdata name1[rb] decided to
 [lb]ns_set get $formdata verb1[rb] [lb]ns_set get $formdata name2[rb]
 with a [lb]ns_set get $formdata noun2[rb].  This made
 [lb]ns_set get $formdata name2[rb] [lb]ns_set get $formdata verb2[rb]
 [lb]ns_set get $formdata name1[rb].
 <p><center>The End</center>
 The form data that made this possible:<br>
 $hrformdata
 </body></html>"
 }
[example_end]



[subsection {Example 3b: pagetcl/genstory}]

The following example script implements the same story generating
function of genstory (the previous example) but is implemented as a
Tcl page instead of a library Tcl function. Save the following
snippet as [term genstory.tcl] as well as the associated HTML file
[term genstory.html] under the pages directory.

[example_begin]
 # Example 3b: Form generation and handling
 #
 # This operation generates a story based on the
 # form data submitted from the form genstory.html.
 #
 # Things to notice:
 #
 # * This file should be stored with the HTML pages
 #   of the server.  When a client requests the URL corresponding
 #   to the file, the NaviServer sets the "conn" variable and
 #   evaluates the Tcl.
 #
 # * An error status (500) is returned if the client doesn't
 #   doesn't pass in any form data.
 #
 # * Form data is stored in an ns_set, and accessed
 #   like any other set (e.g., header data).
 #
 # * A counter is used to loop through all the key
 #   value pairs in the form.
 
 set formdata [lb]ns_conn form[rb]
 
 if {$formdata eq ""} {
    ns_return 200 text/plain "Need form data!"
    return
 }
 
 # Build up a human-readable representation of the form data.
 
 set hrformdata "<dl>"
 set size [lb]ns_set size $formdata[rb]
 for {set i 0} {$i < $size} {incr i} {
    append hrformdata "<dt>[lb]ns_set key $formdata $i[rb]</dt>\
        <dd>[lb]ns_set value $formdata $i[rb]</dd>"
 }
 append hrformdata "</dl>"
 
 ns_return 200 text/html \
 "<html>
 <head>
 <title>The story of [lb]ns_set get $formdata name1[rb] and
 [lb]ns_set get $formdata name2[rb]</title>
 </head>
 <body>
 <h1>The story of [lb]ns_set get $formdata name1[rb] and
 [lb]ns_set get $formdata name2[rb]</h1>
 <p>Once upon a time [lb]ns_set get $formdata name1[rb] and
 [lb]ns_set get $formdata name2[rb] went for a
 walk in the woods looking for a [lb]ns_set get $formdata noun1[rb].
 [lb]ns_set get $formdata name1[rb] was
 feeling [lb]ns_set get $formdata adjective1[rb] because
 [lb]ns_set get $formdata name2[rb] was so
 [lb]ns_set get $formdata adjective2[rb].  So
 [lb]ns_set get $formdata name1[rb] decided to
 [lb]ns_set get $formdata verb1[rb] [lb]ns_set get $formdata name2[rb]
 with a [lb]ns_set get $formdata noun2[rb].  This made
 [lb]ns_set get $formdata name2[rb] [lb]ns_set get $formdata verb2[rb]
 [lb]ns_set get $formdata name1[rb].
 <p><center>The End</center>
 The form data that made this possible:<br>
 $hrformdata
 </body></html>"
[example_end]

Here's the associated HTML file [term genstory.html]:

[example_begin]
 <html>
 <head>
 <title>Automatic Story Generator</title>
 </head>
 <body>
 <h1>Automatic Story Generator</h1>
 <form action='genstory.tcl' method='POST'>
 Noun: <input type='text' name='noun1'><br>
 Noun: <input type='text' name='noun2'><br>
 Name: <input type='text' name='name1'><br>
 Name: <input type='text' name='name2'><br>
 Adjective: <input type='text' name='adjective1'><br>
 Adjective: <input type='text' name='adjective2'><br>
 Verb: <input type='text' name='verb1'><br>
 Verb: <input type='text' name='verb2'><br>
 <p><input type='submit' value='Generate'>
 </form>
 </body>
 </html>
[example_end]


[subsection {Example 4: redirect}]

The following example script shows how to use a NaviServer simple
response command (in this case, ns_returnredirect) and the equivalent
code when sending raw data to the client.

[example_begin]
 # Example 4: Implementing redirects with ns_respond and
 # ns_write
 #
 # /example/not_here uses ns_respond to return an HTTP
 #   redirect to /example/finaldest.
 # /example/not_here2 does the same thing using ns_write
 # /example/not_here3 does the same thing with
 #   ns_returnredirect
 #
 # Things to notice:
 #
 # * When you use ns_write, you need to compose the
 #   entire response.
 #
 # * "ns_conn location" returns the http://hostname
 #   part of the URL that you can use to generate
 #   fully qualified URLs.
 #
 # * ns_returnredirect is a lot simpler than either
 #   ns_respond or ns_write.
 
 ns_register_proc GET /example/not_here  not_here
 ns_register_proc GET /example/not_here2 not_here2
 ns_register_proc GET /example/not_here3 not_here3
 ns_register_proc GET /example/finaldest finaldest
 
 proc not_here {} {
   ns_respond -status 302 -type text/plain \
      -string "Redirection" \
      -headers [lb]ns_set new myheaders \
         Location [lb]ns_conn location[rb]/example/finaldest[rb] \
         [rb]
 }
 
 proc not_here2 {} {
   set content [lb]subst [lb]ns_trim -delimiter | {
        |<html>
        |<head><title>Redirection</title></head>
        |<body>
        |<h1>Redirection</h1>
        |The actual location of what you were looking for is
        |<a href='[lb]ns_conn location[rb]/example/finaldest'> here.</a>
        |</body>
        |</html>}[rb][rb]
 
   ns_write [lb]subst [lb]ns_trim -delimiter | {
        |HTTP/1.0 302 Document follows
        |MIME-Version: 1.0
        |Content-Type: text/html
        |Content-Length: [lb]string length $content[rb]
        |Location: [lb]ns_conn location[rb]/example/finaldest
        |
        |$content
        |}[rb][rb]
 }
 
 proc not_here3 {} {
   ns_returnredirect [lb]ns_conn location[rb]/example/finaldest
 }
 
 proc finaldest {} {
   ns_return 200 text/plain \
     "You have arrived at the final destination."
 }
[example_end]



[subsection {Example 5: desctable}]

The following example script provides a request procedure which
describes the columns of a database table using the NaviServer
"ns_tableinfo" command .

[example_begin]
 # Example 5: Describing a database table
 #
 # /example/describetable prints out a column-by-column
 #   description of a database table.  The database
 #   pool name and table name are specified at the end
 #   of the URL -- e.g.,
 #
 #       /example/describetable/nsdbpool/ns_users
 #
 # Note: You must have the nsdb module loaded into your virtual
 #       server for this example to work.
 #
 # Things to notice:
 #
 # * ns_returnbadrequest returns a nicely formatted message
 #   telling the client they submitted an invalid request.
 #
 # * "ns_conn urlv" returns a Tcl array whose elements are the
 #   slash-delimited parts of the URL.
 #
 # * The describetable function loops through all the columns
 #   and uses "ns_column valuebyindex" to get the type of each
 #   one.
 #
 # * ns_returnnotice nicely formats the return value.
 
 ns_register_proc GET /example/describetable describetable
 
 proc describetable {} {
    if {[lb]ns_conn urlc[rb] != 4} {
        return [lb]ns_returnbadrequest "Missing table name and/or poolname"[rb]
    }
    set pool [lb]lindex [lb]ns_conn urlv] 2[rb]
    if {$pool ni [lb]ns_db pools[rb]} {
        return [lb]ns_returnbadrequest "Pool $pool does not exist"[rb]
    }
    set db [lb]ns_db gethandle $pool[rb]
    set table [lb]lindex [lb]ns_conn urlv] 3[rb]
    if {[lb]info command ns_table[rb] eq ""} {
       set output "Driver [lb]ns_db driver $db[rb] does not support command ns_table"
    } else {
        set tinfo [lb]ns_table info $db $table[rb]
        if {$tinfo eq ""} {
            return [lb]ns_returnbadrequest "Table $table does not exist"[rb]
        }
        set output "<dl>"
        set size [lb]ns_column count $tinfo[rb]
        for {set i 0} {$i < $size} {incr i} {
            append output "<dt>[lb]ns_column name $tinfo $i[rb] \
                    <dd>[lb]ns_column typebyindex $tinfo $i[rb]</dd>"
        }
        append output "</dl><hr>"
    }
    ns_returnnotice 200 "Table $table in pool $pool" $output
 }
[example_end]



[subsection {Example 6: getempsm}]

The following example script shows how to query a table in the
database.

[example_begin]
 # Example 6: Getting data from the database
 #
 # /example/getemps queries a database in the default
 # pool and returns a list of all the employees listed
 # in the employees table.  It assumes a table called
 # employees exists with the column emp_name. 
 #
 # Note: You must have the nsdb module loaded into your virtual
 #       server for this example to work.
 #
 # Things to notice:
 #
 # * Use "ns_db gethandle" to get a handle for the database
 #   from the default database pool of the virtual server.
 #
 # * Use "ns_db select" to query the database and
 #   "ns_db getrow" to retrieve data.
 #
 # * Rows are returned as ns_sets.
 #
 
 ns_register_proc GET /example/getemps getemps
 
 proc getemps {} {
        set ul "<ul>"
        set db [lb]ns_db gethandle [lb]ns_config [lb]ns_dbconfigpath[rb] "DefaultPool"[rb][rb]
        set row [lb]ns_db select $db "select emp_name from employees order by emp_name;"[rb]
        while { [lb]ns_db getrow $db $row] } {
            append ul "<li>[lb]ns_set get $row emp_name[rb] \n"
        }
        append ul "</ul>"
        ns_returnnotice 200 "Employee list" $ul
 }
 
[example_end]



[subsection {Example 7: wincgi}]

The following example script is a simple emulation of the WebSite
WinCGI interface. Notice that this is just a demo, since the NaviServer
modules nscgi provides a much simpler way to interface via CGI (when
this is necessary).

[example_begin]
 #
 # Example 7: simple emulation of the WebSite WinCGI interface
 #
 # This Tcl script emulates the WinCGI interface of the WebSite server.
 # To use, move this file to your Tcl library directory (normally the
 # modules/tcl directory of the NaviServer directory), set the
 # following nsd.ini variables in the [lb]ns\server\<server-name>\wincgi]
 # section, and restart the server.
 #
 # key     default    description
 # ---     -------    -----------
 # prefix  /cgi-win   URL prefix for WinCGI.
 # debug   off        Set to on to keep temp files for debugging.
 # gmtoff  0          Minutes West of GMT for the "GMT Offset" variable.
 # dir     c:\wincgi  Directory of WinCGI programs.
 #
 #
 # Note:  This script is unsupported and not a complete emulation of the
 # WebSite WinCGI interface.  In particular, not all the WinCGI variables
 # are set.  Full support for WinCGI will be incorporated into the nscgi
 # module in a future NaviServer release.
 #
 
 #
 # Fetch the variables from the configuration file.
 #
 global WinCGI
 set WinCGI(section) "ns\\server\\[lb]ns_info server]\\wincgi"
 if {[lb]set WinCGI(prefix) [lb]ns_config $WinCGI(section) prefix[rb][rb] eq ""} {
    set WinCGI(prefix) /cgi-win
 }
 if {[lb]set WinCGI(dir) [lb]ns_config $WinCGI(section) dir[rb][rb] eq ""} {
    set WinCGI(dir) [lb]ns_info home[rb]/$WinCGI(prefix)
 }
 if {[lb]set WinCGI(gmtoff) [lb]ns_config $WinCGI(section) gmtoff[rb][rb] eq ""} {
    set WinCGI(gmtoff) 0
 }
 if {[lb]set WinCGI(debug) [lb]ns_config -bool $WinCGI(section) debug[rb][rb] eq ""} {
    set WinCGI(debug) 0
 }
 
 
 #
 # Register the win-cgi procedure to handle requests for WinCGI executables.
 #
 ns_register_proc POST $WinCGI(prefix)/*.exe win-cgi
 ns_register_proc GET $WinCGI(prefix)/*.exe win-cgi
 
 
 #
 # win-cgi - The Tcl request procedure which emulates WinCGI.
 #
 proc win-cgi {} {
    global WinCGI
 
    # The program is the second part of the WinCGI URL.
    set args [lb]join [lb]split [lb]ns_conn query[rb] &[rb][rb]
    set pgm [lb]lindex [lb]ns_conn urlv[rb] 1[rb]
    regsub -all {\+} $args " " args
    foreach e [lb]split $WinCGI(dir)/$pgm /] {
            if {$e ne ""} {lappend exec $e}
    }
    set exec [lb]join $exec [rb]
    if ![lb]file executable $exec[rb] {
            return [lb]ns_returnnotfound[rb]
    }
 
    # WinCGI requires a few temporary files.
    set ini [lb]ns_mktemp[rb]
    set inp [lb]ns_mktemp[rb]
    set out [lb]ns_mktemp[rb]
 
    # Copy the request content to the input file.
    set fp [lb]open $inp w[rb]
    ns_writecontent $fp
    set len [lb]tell $fp[rb]
    close $fp
 
    # Create the WinCGI variables .ini file.
    set fp [lb]open $ini w[rb]
    puts $fp {[lb]CGI[rb]}
    puts $fp \
 "Request Protocol=HTTP/1.0
 Request Method=[lb]ns_conn method[rb]
 Executable Path=$WinCGI(prefix)/$pgm
 Server Software=[lb]ns_info name]/[lb]ns_info version[rb]
 Server Name=[lb]ns_info name[rb]
 Server Port=[lb]ns_info version[rb]
 Server Admin=[lb]ns_config NaviServer WebMaster[rb]
 CGI Version=CGI/1.2 (Win)
 Remote Address=[lb]ns_conn peeraddr[rb]
 Authentication Method=Basic
 Authentication Realm=[lb]ns_conn location[rb]
 Content Type=application/x-www-form-urlencoded
 Content Length=$len"
    puts $fp ""
    puts $fp {[lb]System[rb]}
    puts $fp \
 "GMT Offset=$WinCGI(gmtoff)
 Debug Mode=Yes
 Output File=$out
 Content File=$inp"
 
    # Set any POST or query form variables.
    puts $fp ""
    puts $fp {[lb]Form Literal[rb]}
    set form [lb]ns_conn form[rb]
    if {$form ne ""} {
            for {set i 0} {$i < [lb]ns_set size $form[rb]} {incr i} {
                    set key [lb]ns_set key $form $i[rb]
                    set value [lb]ns_set value $form $i[rb]
                    puts $fp "$key=$value"
            }
    }
 
    # Set the accept headers and accumulate the extra headers.
    puts $fp ""
    puts $fp {[lb]Accept[rb]}
    set headers [lb]ns_conn headers[rb]
    set extras ""
    for {set i 0} {$i < [lb]ns_set size $headers[rb]} {incr i} {
            set key [lb]ns_set key $headers $i[rb]
            set ukey [lb]string toupper $key[rb]
            set value [lb]ns_set value $headers $i[rb]
            if {$ukey eq "ACCEPT"} {
                    puts $fp "$value=Yes"
            } elseif {$key ne "CONTENT-LENGTH" && $key ne "CONTENT-TYPE"} {
                    append extras "$key=$value\n"
            }
    }
    puts $fp ""
    puts $fp {[lb]Extra Headers[rb]}
    puts $fp $extras
    close $fp
 
    # Execute the WinCGI program.
    # NB:  "catch" the exec and open because a WinCGI
    # program can be misbehaved, returning a nonzero
    # exit status or not creating the output file.
    catch {exec "$exec $ini $inp $out $args"}
    if [lb]catch {set fp [lb]open $out]}[rb] {
            ns_returnerror 500 "WinCGI exec failed"
    } else {
            set type text/html
            set status 200
            while {[lb]gets $fp line[rb] > 0} {
                    set line [lb]string trim $line[rb]
                    if {$line eq ""} break
                    set head [lb]split $line :[rb]
                    set key [lb]string tolower [lb]string trim [lb]lindex $head 0[rb][rb][rb]
                    set value [lb]string trim [lb]lindex $head 1[rb][rb]
                    if {$key eq "content-type"} {
                            set type $value
                    } elseif {$key eq "location"} {
                            set location $value
                    } elseif {$key eq "status"} {
                            set status $status
                    }
            }
            set page [lb]read $fp[rb]
            close $fp
 
            if [lb]info exists location[rb] {
                    ns_returnredirect $location
            } else {
                    ns_return $status $type $page
            }
    }
 
    if $WinCGI(debug) {
            ns_log Notice "CGI $pgm: ini: $ini, inp: $inp, out: $out"
    } else {
            file delete -- $ini
            file delete -- $inp
            file delete -- $out
    }
 }
[example_end]

[keywords ns_set nsdb blueprint configuration loading tcl-modules]

[manpage_end]
